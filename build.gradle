/* ###
 * IP: GHIDRA
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// Builds a Ghidra Extension for a given Ghidra installation.
//
// An absolute path to the Ghidra installation directory must be supplied either by setting the 
// GHIDRA_INSTALL_DIR environment variable or Gradle project property:
//
//     > export GHIDRA_INSTALL_DIR=<Absolute path to Ghidra> 
//     > gradle
//
//         or
//
//     > gradle -PGHIDRA_INSTALL_DIR=<Absolute path to Ghidra>
//
// Gradle should be invoked from the directory of the project to build.  Please see the
// application.gradle.version property in <GHIDRA_INSTALL_DIR>/Ghidra/application.properties
// for the correction version of Gradle to use for the Ghidra installation you specify.

//----------------------START "DO NOT MODIFY" SECTION------------------------------
def ghidraInstallDir

if (System.env.GHIDRA_INSTALL_DIR) {
	ghidraInstallDir = System.env.GHIDRA_INSTALL_DIR
}
else if (project.hasProperty("GHIDRA_INSTALL_DIR")) {
	ghidraInstallDir = project.getProperty("GHIDRA_INSTALL_DIR")
}
else {
	ghidraInstallDir = "/opt/ghidra"
}

task distributeExtension {
	group = "Ghidra"

	apply from: new File(ghidraInstallDir).getCanonicalPath() + "/support/buildExtension.gradle"
	dependsOn ':buildExtension'
}
//----------------------END "DO NOT MODIFY" SECTION-------------------------------

repositories {
	// Declare dependency repositories here.  This is not needed if dependencies are manually 
	// dropped into the lib/ directory.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html for more info.
	// Ex: mavenCentral()
}

dependencies {
	// Any external dependencies added here will automatically be copied to the lib/ directory when
	// this extension is built.	
}

// Exclude additional files from the built extension
// Ex: buildExtension.exclude '.idea/**'


task installLocalExtension {
    dependsOn buildExtension
    doLast {
		def ghidraHomeDir
		if (System.env.GHIDRA_HOME) 
			ghidraHomeDir = System.env.GHIDRA_HOME
		else if (project.hasProperty("GHIDRA_HOME"))
			ghidraHomeDir = project.getProperty("GHIDRA_HOME")
		else ghidraHomeDir = "/opt/ghidra/Ghidra"
        
        // Читаем application.properties
        def propsFile = new File("$ghidraHomeDir/application.properties")
        if (!propsFile.exists())
            throw new GradleException("Cannot find application.properties at: ${propsFile.absolutePath}")
        
        Properties appProps = new Properties()
        propsFile.withInputStream { appProps.load(it) }
        
        def version = appProps.getProperty("application.version", "11.4.2")
        def releaseName = appProps.getProperty("application.release.name", "DEV")
        
        // Составляем путь к Extensions
        def ghidraExtDir = System.getProperty("user.home") + "/.config/ghidra/ghidra_${version}_${releaseName}/Extensions/"
        
        def sourceZip = fileTree("dist").include("*.zip").singleFile
        
        delete ghidraExtDir
        mkdir ghidraExtDir
        
        copy {
            from zipTree(sourceZip)
            into ghidraExtDir
        }
    }
}